# Maintainer: Reza Mousavi <iamrezamousavi at gmail dot com>

pkgname='ghermez-git'
pkgver=3.2.0.r101.g7ce2d57
pkgrel=1
pkgdesc="Qt front-end and Rust back-end for aria2 download manager (Github version)."
arch=('any')
url="https://github.com/IamRezaMousavi/ghermez"
license=('GPL3')
depends=('aria2' 'ffmpeg' 'libnotify' 'libpulse' 'pyside6' 'python-maturin' 'python-psutil' 'python-requests' 'python-setproctitle' 'qt6-svg' 'sound-theme-freedesktop' 'youtube-dl' )
makedepends=('git' 'python-setuptools')
optdepends=('adwaita-qt6: for using adwaita style in GTK based Desktops.')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/IamRezaMousavi/ghermez.git")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname%-git}
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd ${pkgname%-git}
    python setup.py build
}

package() {
    cd ${pkgname%-git}
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
# vim:set ts=4 sw=4:
